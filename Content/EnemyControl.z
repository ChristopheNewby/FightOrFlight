class EnemyController : ZilchComponent
{
    [Property]
    var PaceSpeed : Real = 0;
    [Property]
    var MaxMoveDistance : Real = 10.0;
    var PaceDirection : Real3 = Real3( 0.0, 0.0, 0.0);
    var StartPosition : Real3 = Real3();
    
    [Property]
    var TargetObject : CogPath = null;
    [Property]
    var ChaseSpeed : Real = 3.0;
    var ChaseTriggerDistance : Real = 10.0;
    var DistanceFromTarget : Real = 0.0;
    var ChaseDirection : Real3 = Real3();
    var TargetIsWithinRange : Boolean = false;
    
    
    [Dependency]
    var Transform : Transform = null;
    [Dependency]
    var Sprite : Sprite = null;
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        this.StartPosition = this.Owner.Transform.Translation;

    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        this.TargetIsWithinRange = false;
        
        if (this.TargetObject.Cog != null)
        {
            this.CalculateChaseDirectionAndDistance();
            this.TargetIsWithinRange = (this.DistanceFromTarget <= this.ChaseTriggerDistance);
            
            if (this.TargetIsWithinRange)
            {
                this.ChaseTarget(event);
            }
            else
            {
                this.PaceBackAndForth(event);
            }
        }
        else
        {
            this.PaceBackAndForth(event);
        }
    }
    
    function PaceBackAndForth(event : UpdateEvent)
    {
        var displacement = this.Owner.Transform.Translation - this.StartPosition;
        var distanceFromStart = Math.Length(displacement);
        
        if (distanceFromStart >= this.MaxMoveDistance)
        {
            this.PaceDirection = -displacement;
        }
        this.PaceDirection = Math.Normalize(this.PaceDirection);
        this.Owner.Transform.Translation += this.PaceDirection * event.Dt * this.PaceSpeed;
    }
    
    function CalculateChaseDirectionAndDistance()
    {
        this.ChaseDirection = this.TargetObject.Cog.Transform.Translation - this.Owner.Transform.Translation;
        this.DistanceFromTarget = Math.Length(this.ChaseDirection);
        this.ChaseDirection = Math.Normalize(this.ChaseDirection);
    }
    
    function ChaseTarget(event : UpdateEvent)
    {
        this.Owner.Transform.Translation += this.ChaseDirection * event.Dt * this.ChaseSpeed;
    }
}